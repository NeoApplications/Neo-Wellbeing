import java.nio.file.Files

plugins {
	id 'com.android.application'
	id 'org.jetbrains.kotlin.android'
}

android {
	namespace 'org.eu.droid_ng.wellbeing'
	compileSdk 33

	defaultConfig {
		applicationId "org.eu.droid_ng.wellbeing"
		minSdk 29
		targetSdk 33
		versionCode 3
		versionName "0.2.1"
	}


	signingConfigs {
		release {
			if (project.hasProperty('RELEASE_KEY_ALIAS')) {
				storeFile file(RELEASE_STORE_FILE)
				storePassword RELEASE_STORE_PASSWORD
				keyAlias RELEASE_KEY_ALIAS
				keyPassword RELEASE_KEY_PASSWORD
				v1SigningEnabled true
				v2SigningEnabled true
			}
		}
	}

	buildTypes {
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			if (project.hasProperty('RELEASE_KEY_ALIAS')) {
				signingConfig signingConfigs.release
			}
		}
	}

	sourceSets.main.java.srcDirs += ['src/main/java_magisk']

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_11
		targetCompatibility JavaVersion.VERSION_11
	}
	kotlin {
		jvmToolchain(11)
	}
}

dependencies {
	implementation project(':shared')

	def lifecycle_version = "2.6.1" // Version needs to match
	implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
	implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

	implementation 'androidx.recyclerview:recyclerview:1.3.0'
	implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
	implementation 'androidx.preference:preference:1.2.0'
	implementation 'androidx.appcompat:appcompat:1.6.1'
	implementation 'com.google.android.material:material:1.8.0'

	implementation 'com.github.AppDevNext:AndroidChart:3.1.0.15'
}

def magiskModuleProp = [
		id           : "neo_wellbeing",
		name         : "Neo Wellbeing systemless",
		version      : android.defaultConfig.versionName,
		versionCode  : android.defaultConfig.versionCode,
		minApi       : android.defaultConfig.minSdk,
		support      : "https://github.com/NeoApplications/Neo-Wellbeing",
		config       : "org.eu.droid_ng.wellbeing",
		author       : "nift4",
		description  : "Neo Wellbeing is an open source reimplementation of Wellbeing"
]

def outDir = rootProject.buildDir
def magiskDir = file("$outDir/magisk_module")
def zipName = "NeoWellbeing-${android.defaultConfig.versionName}.zip"
def zipFile = new File(outDir, zipName)

task assembleMagiskModule(type: Task) {
	dependsOn(":app:assembleRelease")
	dependsOn(":framework:assembleRelease")
	doLast {
		delete(magiskDir)
		magiskDir.mkdirs()
		def modulePropText = ""
		magiskModuleProp.each { k, v -> modulePropText += "$k=$v\n" }
		file("$magiskDir/module.prop").text = modulePropText
		file("$magiskDir.path/system/priv-app/NeoWellbeing").mkdirs()
		Files.copy(file("$rootDir/app/build/outputs/apk/release/app-release.apk").toPath(),
				file("${magiskDir.path}/system/priv-app/NeoWellbeing/NeoWellbeing.apk").toPath())
		file("$magiskDir.path/system/priv-app/NeoWellbeingFramework").mkdirs()
		Files.copy(file("$rootDir/framework/build/outputs/apk/release/framework-release.apk").toPath(),
				file("${magiskDir.path}/system/priv-app/NeoWellbeingFramework/NeoWellbeingFramework.apk").toPath())
		file("$magiskDir.path/system/product/overlay/NeoWellbeingOverlay").mkdirs()
		Files.copy(file("$rootDir/NeoWellbeingOverlay/overlay.apk").toPath(),
				file("${magiskDir.path}/system/product/overlay/NeoWellbeingOverlay/NeoWellbeingOverlay.apk").toPath())
		file("$magiskDir.path/system/etc/permissions").mkdirs()
		Files.copy(file("$rootDir/app/src/main/privapp-permissions-wellbeing.xml").toPath(),
				file("${magiskDir.path}/system/etc/permissions/privapp-permissions-wellbeing.xml").toPath())
		file("$magiskDir.path/META-INF/com/google/android").mkdirs()
		file("$magiskDir.path/META-INF/com/google/android/updater-script").text = "#MAGISK"
		Files.copy(file("$rootDir/app/update-binary").toPath(),
				file("$magiskDir.path/META-INF/com/google/android/update-binary").toPath())
		Files.copy(file("$rootDir/app/customize.sh").toPath(),
				file("$magiskDir.path/customize.sh").toPath())
	}
}

task zipMagiskModule(type: Zip) {
	from magiskDir
	archiveName zipName
	destinationDir outDir
	dependsOn(":app:assembleMagiskModule")
}

task pushMagiskModule(type: Exec) {
	commandLine('adb', 'push', zipFile.absolutePath, "/sdcard/Documents/" + zipName)
	dependsOn(":app:zipMagiskModule")
}

task testMagiskModule(type: Exec) {
	commandLine('adb', 'shell', 'su', '-c',
			"magisk --install-module /sdcard/Documents/" + zipName +
					" && (/system/bin/svc power reboot || /system/bin/reboot)")
	dependsOn(":app:pushMagiskModule")
}
